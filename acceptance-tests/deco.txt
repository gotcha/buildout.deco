*** Settings ***
Resource  plone.txt

*** Keywords ***

Wait until notification
    [Arguments]  ${text}

    Wait until keyword succeeds  1  5  Frame should contain  plone-cmsui-notifications  ${text}

Click ${name} menu link
    Menu should contain  ${name}
    Click link  ${name}  don't wait

Click ${name} menu button
    Menu should contain  ${name}
    Click button  ${name}  don't wait

Should be above
    [Arguments]  ${locator1}  ${locator2}

    ${locator1-position} =  Get vertical position  ${locator1}
    ${locator2-position} =  Get vertical position  ${locator2}
    Should be true  ${locator1-position} < ${locator2-position}

Wait for Deco edit UI    
    Wait until page contains element  css=.deco-selected-tile

Deco edit
    Click Edit menu link
    Wait for Deco edit UI

Start dragging tile
    [Arguments]  ${locator}
    
    Mouse over  ${locator}
    ${DRAG_HANDLE} =  Catenate  ${locator}  .deco-drag-handle
    Wait until page contains element  ${DRAG_HANDLE}
    ${vertical-position} =  Get vertical position  ${locator}
    ${vertical-position-moved} =  Evaluate  ${vertical-position} + 5
    ${horizontal-position} =  Get horizontal position  ${locator}
    ${horizontal-position-moved} =  Evaluate  ${horizontal-position} + 5
    Mouse down at  ${DRAG_HANDLE}  ${horizontal-position},${vertical-position}
    Mouse move at  ${DRAG_HANDLE}  ${horizontal-position-moved},${vertical-position-moved}

Drop bottom of 
    [Arguments]  ${locator}
    
    ${left} =  Get horizontal position  ${locator}
    ${width} =  Get width  ${locator}
    ${top} =  Get vertical position  ${locator}
    ${height} =  Get height  ${locator}
    ${drop-h} =  Evaluate  ${left} + ${width} / 2
    ${drop-v} =  Evaluate  ${top} + ${height} - 1
    Mouse move at  ${locator}  ${drop-h},${drop-v}
    Mouse up at  ${locator}  ${drop-h},${drop-v}

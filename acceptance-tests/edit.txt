*** Settings ***
Resource  deco.txt

Suite setup  Log in  admin  admin

Test setup  Edit deco page

*** Variables ***
${TITLE_TILE} =  css=.deco-plone\\.app\\.standardtiles\\.title-tile
${DESCRIPTION_TILE} =  css=.deco-plone\\.app\\.standardtiles\\.description-tile
${DESCRIPTION_TILE_DRAG} =  ${DESCRIPTION_TILE} .deco-drag-handle


*** Test cases ***

Enter edit mode
    [Tags]  deco

    No operation

Switch tile
    [Tags]  deco
 
    Select Description tile
    
    ${description} =  Get text  css=.deco-selected-tile .documentDescription
    Should be equal  ${description}  for acceptance test

Delete tile
    [Tags]  deco
   
    Delete Description tile
    
Cancel edition
    [Tags]  deco

    Delete Description tile

    Click Cancel menu button
    Wait until notification  Edit cancelled
    
    Page should contain  for acceptance test

Drag and drop description tile
    [Tags]  deco
 
    Should be above  ${TITLE_TILE}  ${DESCRIPTION_TILE}

    Start dragging tile  ${DESCRIPTION_TILE}
    Mouse move  ${TITLE_TILE}
    Mouse up  ${TITLE_TILE}

    Should be above  ${DESCRIPTION_TILE}  ${TITLE_TILE}

Drag and drop title tile
    [Tags]  deco  current 
 
    Should be above  ${TITLE_TILE}  ${DESCRIPTION_TILE}

    Select Description tile

    Start dragging tile  ${TITLE_TILE}

    Drop bottom of  ${DESCRIPTION_TILE}

    Should be above  ${DESCRIPTION_TILE}  ${TITLE_TILE}

*** Keywords ***

Edit deco page
    Go to  ${PLONE_URL}/deco-page
    Page should contain  Deco page
    Deco edit

Select Description tile
    Wait until page contains element  css=.documentDescription
    
    Click element  css=.documentDescription  don't wait
    Wait until page contains element  css=.deco-selected-tile .documentDescription

Delete Description tile
    Select Description tile
    
    Wait until page contains element  css=.deco-close-icon

    Click element  css=.deco-close-icon  don't wait
    Wait until notification  Application tile removed
